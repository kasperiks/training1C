
Процедура ДействияФормыСформировать(Кнопка)
	ЭлементыФормы.Результат.Очистить();
	
	Скрипт=Новый COMОбъект("MSScriptControl.ScriptControl");
	Скрипт.Language="javascript";
	ТочкаСтарт=Скрипт.eval("new Date().getTime()");
	
	КомпоновщикМакета=Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных=КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,КомпоновщикНастроек.Настройки,ДанныеРасшифровки);
	ПроцессорКомпоновкиДанных=Новый ПроцессорКомпоновкиДанных;
	// Здесь, при необходимости, следует добавить инициализацию внешних данных, если используется источник данных типа Объект.
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,,ДанныеРасшифровки,Истина);
	ПроцессорВывода=Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ЭлементыФормы.Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	Если СвернутьГруппировки Тогда
		ЭлементыФормы.Результат.ПоказатьУровеньГруппировокСтрок(0);
	КонецЕсли;
	
	ТочкаФиниш=Скрипт.eval("new Date().getTime()");
	ЭлементыФормы.НадписьВремяФормирования.Заголовок=" Время формирования отчета: "+ПолучитьПредставлениеВремени(ТочкаФиниш-ТочкаСтарт)+" мс";
	
	ЭлементыФормы.ДействияФормы.Кнопки.БазовыеНастройки.Пометка=Ложь;
	ЭлементыФормы.ПанельОсновная.ТекущаяСтраница=ЭлементыФормы.ПанельОсновная.Страницы.Отчет;
КонецПроцедуры

Процедура ДействияФормыНастройки(Кнопка)
	Формировать=ПолучитьФорму("ФормаНастройки").ОткрытьМодально();
	Если Формировать<>Неопределено Тогда
		ДействияФормыСформировать(ЭлементыФормы.ДействияФормы.Кнопки.Сформировать);
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьПредставлениеВремени(Миллисекунды)
	КолВоЧасов=Цел(Миллисекунды/(60*60*1000));
	Остаток=Миллисекунды-КолВоЧасов*60*60*1000;
	КолВоМинут=Цел(Остаток/(60*1000));
	Остаток=Остаток-КолВоМинут*60*1000;
	КолВоСекунд=Цел(Остаток/1000);
	КолВоМиллисекунд=Остаток-КолВоСекунд*1000;
	Возврат Формат(КолВоЧасов,"ЧЦ=3;ЧН=;ЧВН=")+":"+Формат(КолВоМинут,"ЧЦ=2;ЧН=;ЧВН=")+":"+Формат(КолВоСекунд,"ЧЦ=2;ЧН=;ЧВН=")+":"+Формат(КолВоМиллисекунд,"ЧЦ=3;ЧН=;ЧВН=");
КонецФункции

Процедура ДействияФормыБазовыеНастройки(Кнопка)
	ПанельОсновная=ЭлементыФормы.ПанельОсновная;
	Если Кнопка.Пометка Тогда
		ПанельОсновная.ТекущаяСтраница=ПанельОсновная.Страницы.Отчет;
	Иначе
		ПанельОсновная.ТекущаяСтраница=ПанельОсновная.Страницы.Настройки;
	КонецЕсли;
	Кнопка.Пометка=НЕ Кнопка.Пометка;
КонецПроцедуры

Процедура НажатиеКнопкиМеню(Элемент)
	ВыбраннаяПанель=ЭлементыФормы["Панель"+Элемент.Имя];
	КартинкаКнопки=ЭлементыФормы["Картинка"+Элемент.Имя];
	Если ОткрытаяПанель=Неопределено Тогда
		ВыбраннаяПанель.Свертка=РежимСверткиЭлементаУправления.Нет;
		КартинкаКнопки.Картинка=ЭлементыФормы.СвернутьМеню.Картинка;
		ОткрытаяПанель=ВыбраннаяПанель;
	ИначеЕсли ВыбраннаяПанель=ОткрытаяПанель Тогда
		ВыбраннаяПанель.Свертка=РежимСверткиЭлементаУправления.Верх;
		КартинкаКнопки.Картинка=ЭлементыФормы.РазвернутьМеню.Картинка;
		ОткрытаяПанель=Неопределено;
	Иначе
		ВыбраннаяПанель.Свертка=РежимСверткиЭлементаУправления.Нет;
		КартинкаКнопки.Картинка=ЭлементыФормы.СвернутьМеню.Картинка;
		ОткрытаяПанель.Свертка=РежимСверткиЭлементаУправления.Верх;
		ЭлементыФормы["Картинка"+Сред(ОткрытаяПанель.Имя,7)].Картинка=ЭлементыФормы.РазвернутьМеню.Картинка;
		ОткрытаяПанель=ВыбраннаяПанель;
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	ЭлементыФормы.ПанельПараметры.Свертка=РежимСверткиЭлементаУправления.Верх;
	ЭлементыФормы.ПанельОтбор.Свертка=РежимСверткиЭлементаУправления.Верх;
	ЭлементыФормы.ПанельОформление.Свертка=РежимСверткиЭлементаУправления.Верх;
	РезультатПриАктивизацииОбласти(0);
КонецПроцедуры

Процедура РезультатПриАктивизацииОбласти(Элемент)
	КолЯчеекВВыделении = 0;
	ОбщСумма = ПолучитьСуммуЧиселВыделеннойОбластиТабДок(ЭлементыФормы.Результат);
	КолЯчеекВВыделении = ПолучитьЧислоЯчеекВыделеннойОбластиТабДок(ЭлементыФормы.Результат);
	ОбщийТекст = "" + ОбщСумма + " (" + КолЯчеекВВыделении + " яч.)";
	Если КолЯчеекВВыделении > 1 Тогда
		ЭлементыФормы.ПанельИнфо.Свертка = РежимСверткиЭлементаУправления.Нет;
	Иначе
		ЭлементыФормы.ПанельИнфо.Свертка = РежимСверткиЭлементаУправления.Низ;
	КонецЕсли;
	ЭлементыФормы.ИнформационнаяНадписьДляМакета.Значение = ОбщийТекст;
КонецПроцедуры

